buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'me.tatarka:gradle-retrolambda:2.5.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    jcenter()
}

String getJavaHome(version) {
    def out = new ByteArrayOutputStream()
    def result = exec {
        commandLine '/usr/libexec/java_home', '-v', version
        standardOutput = out
    }
    out.toString().trim()
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId 'testrx.java8test.app'
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName '1.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

retrolambda {
    jdk(System.getProperty('retrolambda.jdk') ?:
            System.getProperty('jdk8.home') ?:
                    System.getenv('JAVA8_HOME') ?:
                            getJavaHome('1.8') ?:
                                    System.getenv('JAVA_HOME'))
    oldJdk(System.getProperty('retrolambda.oldJdk') ?:
            System.getProperty('jdk7.home') ?:
                    System.getenv('JAVA7_HOME') ?:
                            getJavaHome('1.7'))
    javaVersion JavaVersion.VERSION_1_7
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'org.functionaljava:functionaljava:4.2'
}
